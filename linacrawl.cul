( $Id$ )
( Copyright{2000}: Albert van der Horst, HCC FIG Holland by GNU Public License)
( Uses Richard Stallmans convention. Uppercased word are parameters.    )

HEX INIT-ALL
INCLUDE elf.cul

0804,A0DC EQU semis
0804,B7AC EQU semiscode
0804,A664 EQU docol
0804,F82C EQU last_dea          \ TASK
0804,95C4 EQU last_dea_envir

\ Align an address in the host space.
: hALIGN 1- 3 OR 1+ ;

CREATE NAME-BUFFER 0 , 256 ALLOT
\ Prefix to NAME a prefix, return prefixed NAME in a static buffer.
: PRE-PEND NAME-BUFFER $! NAME-BUFFER $+! NAME-BUFFER $@ ;

\ Transform NAME into: NAMELABEL (prepended "N_").
: >N_  "N_" PRE-PEND ;

\ ADDRESS points to a valid name. Add a label to address the name.
: ADD-NAME-LABEL   DUP th $@ >N_ 2DUP TYPE LABELED ;

\ ADDRESS points to a valid dea. Add a label to address the dea(xt)
\ and the datafield.
\ Assume that just before the dea's name has been analysed.
: ADD-DEA-LABEL   &X NAME-BUFFER CELL+ C!   DUP NAME-BUFFER $@ LABELED
                   &H NAME-BUFFER CELL+ C!
      4 + L@ NAME-BUFFER $@ LABELED ;

\ ADDRESS points to a valid name.
\ Add an anonymous section to disassemble the name.
: ADD-NAME-SECTION   DUP L@ hALIGN OVER + 4 + -d$- ;

\ ADDRESS points to a dea.
\ Add an anonymous section to disassemble the dea.
: ADD-DEA-SECTION   DUP 18 + -dl- ;

\ Add the information that ADDRESS is a nfa.
: IS-A-NAME L@ DUP IF DUP ADD-NAME-LABEL ADD-NAME-SECTION _ THEN DROP ;

\ The CONTENT of address indicates : "This IS not yet the end of
\ high level code".
: STILL-CODE?   >R   R@ semis <>  R@ semiscode <>   AND RDROP ;

\ Add the information that ADDRESS points to high level code.
: IS-HIGH-LEVEL DUP BEGIN DUP L@ STILL-CODE? WHILE 4 + REPEAT 4 + -dl- ;

\ For CFA : "it POINTS to the high level interpreter"
: IS-DOCOL?   L@ docol = ;

\ Accumulate the information that ADDRESS contains a code field address.
: IS-A-CFA   DUP L@ CRAWL   DUP IS-DOCOL? IF 4 + L@ IS-HIGH-LEVEL _ THEN DROP ;

\ Accumulate the information that DEA is a dea.
: IS-A-DEA   DUP 8 + L@ 1 AND IF DROP EXIT THEN \ Dummy field
             DUP IS-A-CFA   DUP 10 + IS-A-NAME
             DUP ADD-DEA-SECTION   ADD-DEA-LABEL ;

\ Accumulate the information from DEA as a wid, follow the link field.
: CRAWL-WID BEGIN DUP IS-A-DEA 0C + L@ DUP 0= UNTIL DROP ;

last_dea_envir CRAWL-WID
last_dea CRAWL-WID

SORT-ALL
PLUG-HOLES
MAKE-CUL
DISASSEMBLE-ALL
